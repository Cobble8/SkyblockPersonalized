import dev.architectury.pack200.java.Pack200Adapter
plugins {
    id 'java'
    id 'idea'
    id 'gg.essential.loom' version '0.10.0.+'
    id 'dev.architectury.architectury-pack200' version '0.1.3'
}


version = ""
group= "com.cobble.sbp"
archivesBaseName = "[1.8.9] SkyblockPersonalized ["+mod_version+"]"


tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

configurations {
    embed
    implementation.extendsFrom(embed)
}

java {
    toolchain.languageVersion.set(JavaLanguageVersion.of(8))
}

loom {
    launchConfigs {
        client {
            arg("--mixin", mod_id+".mixins.json")
            arg("--tweakClass", "org.spongepowered.asm.launch.MixinTweaker")
        }
    }
    forge {
        pack200Provider.set(new Pack200Adapter())
        mixinConfig(mod_id+'.mixins.json')
    }
    mixin {
        defaultRefmapName.set("sbp.mixins.refmap.json")
    }
}

repositories {
    maven { url 'https://repo.spongepowered.org/maven/' }
    mavenCentral()
}

dependencies {
    minecraft("com.mojang:minecraft:1.8.9")
    mappings("de.oceanlabs.mcp:mcp_stable:22-1.8.9")
    forge("net.minecraftforge:forge:1.8.9-11.15.1.2318-1.8.9")
    implementation('org.spongepowered:mixin:0.7.11-SNAPSHOT')
    annotationProcessor "net.fabricmc:sponge-mixin:0.11.4+mixin.0.8.5"
}

jar {
    from configurations.embed.collect { it.isDirectory() ? it : zipTree(it) }
    manifest.attributes(
            "TweakClass": "org.spongepowered.asm.launch.MixinTweaker",
            "MixinConfigs": mod_id+".mixins.json",
            "FMLCorePluginContainsFMLMod": true,
            "ForceLoadAsMod": true
    )
}


processResources {
    inputs.property "version", project.version
    filesMatching(['mcmod.info']) {
        expand 'version':project.version
    }
}

sourceSets.main {
    output.setResourcesDir(file("$buildDir/classes/java/main"))
}


